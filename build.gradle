plugins {
    id 'net.researchgate.release' version '2.8.1'
}

wrapper {
    gradleVersion = '6.2.2'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group = 'net.researchgate'

    sourceCompatibility = 11
    targetCompatibility = 11

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/researchgate/restler"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }

    repositories {
        mavenCentral()
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    test {
        maxHeapSize = '2014m'
        testLogging.showStandardStreams = true
    }

    configurations.all {
        resolutionStrategy {
            force 'ch.qos.logback:logback-core:1.2.3'
            force 'org.slf4j:slf4j-api:1.7.36'
        }
    }
}

project(":restler-core") {
    archivesBaseName = 'restler-core'

    configurations {
        all*.exclude group: 'org.apache.solr'
        all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        all*.exclude module: 'slf4j-jdk14'
        all*.exclude module: 'log4j'
    }

    dependencies {
        compile group: 'dev.morphia.morphia', name: 'morphia-core', version: '2.4.4'
        compile group: 'org.mongodb', name: 'mongodb-driver-sync', version: '4.10.2'

        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.2'
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: ' 2.14.2'

        compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0.1'
        compile group: 'com.google.guava', name: 'guava', version: '18.0'
        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
        compile group: 'commons-collections', name: 'commons-collections', version: '3.2.1'

        compile 'io.swagger.core.v3:swagger-annotations:2.0.1'

        compile group: 'com.google.inject', name: 'guice', version: '4.2.2'

        runtime group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.7'

        testCompile group: 'junit', name: 'junit', version: '4.11'
        testCompile group: 'org.mockito', name: 'mockito-all', version: '1.+'
        testCompile group: 'org.glassfish.jersey.core', name: 'jersey-common', version: '2.23.1'
        testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
        testImplementation group: 'org.testcontainers', name: 'testcontainers', version: '1.17.3'
        testImplementation group: 'org.testcontainers', name: 'mongodb', version: '1.17.3'
    }
}

project(":restler-service") {
    apply plugin: 'application'

    ext {
        dropwizardVersion = '1.0.2'
        metricsVersion = '3.1.2'
        guicierVersion = '1.0.0.4'
    }

    archivesBaseName = 'restler-service'
    mainClassName = 'net.researchgate.restler.RestlerApplication'

    dependencies {
        compile(project(':restler-core'))

        compile group: 'io.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
        compile group: 'com.hubspot.dropwizard', name: 'dropwizard-guicier', version: guicierVersion

        testCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: dropwizardVersion

        testCompile group: 'org.mockito', name: 'mockito-all', version: '1.+'
        testCompile group: 'junit', name: 'junit', version: '4.11'
        testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.6.5'
        testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: '1.6.5'
        testImplementation group: 'org.testcontainers', name: 'testcontainers', version: '1.17.3'
        testImplementation group: 'org.testcontainers', name: 'mongodb', version: '1.17.3'
    }

    test {
        forkEvery = 1
    }

}

release {
    buildTasks = []
}

beforeReleaseBuild.dependsOn subprojects.build
afterReleaseBuild.dependsOn subprojects.publish
