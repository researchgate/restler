apply plugin: 'rg-multi-project'

buildscript {
    repositories {
        //jcenter()
        maven {
            url 'https://nexus3.rgoffice.net/repository/maven-public/'
        }
    }
    dependencies {
        classpath group: 'net.researchgate', name: 'commons-gradle', version: '4.+'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}
//
//plugins {
//    id 'org.gradle.base'
//    id 'net.researchgate.release' version '2.8.1'
//}


wrapper {
    gradleVersion = '5.6.4'
}

repositories {
    mavenCentral()
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.jfrog.bintray'

    group = 'net.researchgate'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    bintray {
        dryRun = false // Whether to run this as dry-run, without deploying
        publish = true //If version should be auto published after an upload

        user = project.hasProperty('bintrayUser') ? bintrayUser : ''
        key = project.hasProperty('bintrayApiKey') ? bintrayApiKey : ''
        configurations = ['archives']
        pkg {
            name = 'restler'
            repo = 'maven'
            userOrg = 'researchgate'
            licenses = ['MIT']
            vcsUrl = 'https://github.com/researchgate/restler.git'
            issueTrackerUrl = 'https://github.com/researchgate/restler/issues'
            githubRepo = 'researchgate/restler'

            version {
                name = rootProject.version
                vcsTag = rootProject.version
                released = new Date()
            }
        }
    }

    repositories {
        mavenCentral()
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    test {
        maxHeapSize = '2014m'
        testLogging.showStandardStreams = true
    }
}

project(":restler-core") {
//    archivesBaseName = 'restler-core'
//
//    configurations {
//        all*.exclude group: 'org.apache.solr'
//        all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
//        all*.exclude module: 'slf4j-jdk14'
//        all*.exclude module: 'log4j'
//    }

    dependencies {
        compile group: 'io.dropwizard.metrics', name: 'metrics-annotation', version: '3.1.2'

        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.3'
        compile group: 'org.mongodb.morphia', name: 'morphia', version: '1.3.2'

        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.2.3'
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.2.3'

        compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0.1'
        compile group: 'com.google.guava', name: 'guava', version: '18.0'
        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
        compile group: 'commons-collections', name: 'commons-collections', version: '3.2.1'

        runtime group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.7'

        testCompile group: 'junit', name: 'junit', version: '4.11'
        testCompile group: 'org.mockito', name: 'mockito-all', version: '1.+'
        testCompile group: 'org.glassfish.jersey.core', name: 'jersey-common', version: '2.23.1'
    }
}

project(":restler-service") {
    apply plugin: 'application'

    ext {
        dropwizardVersion = '1.0.2'
        metricsVersion = '3.1.2'
        guicierVersion = '1.0.0.4'
    }

    archivesBaseName = 'restler-service'
    mainClassName = 'net.researchgate.restler.RestlerApplication'

    dependencies {
        compile(project(':restler-core'))

        compile group: 'io.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
        compile group: 'com.hubspot.dropwizard', name: 'dropwizard-guicier', version: guicierVersion

        testCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: dropwizardVersion

        testCompile group: 'org.mockito', name: 'mockito-all', version: '1.+'
        testCompile group: 'junit', name: 'junit', version: '4.11'
        testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.6.5'
        testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: '1.6.5'
        testCompile group: "de.flapdoodle.embed", name: "de.flapdoodle.embed.mongo", version: "1.50.5"
    }

    test {
        forkEvery = 1
    }

}

release {
    buildTasks = []
}

beforeReleaseBuild.dependsOn subprojects.build
afterReleaseBuild.dependsOn subprojects.bintrayUpload
